apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
// Find hermesc in pnpm's node_modules structure
def findHermesc() {
    def rootDir = file("../../../..").absolutePath  // monorepo root
    def mobileDir = file("../..").absolutePath      // apps/mobile

    def candidates = [
        // Standard npm/yarn location (from apps/mobile)
        "${mobileDir}/node_modules/hermes-compiler/hermesc/%OS-BIN%/hermesc",
        // pnpm hoisted location (monorepo root)
        "${rootDir}/node_modules/hermes-compiler/hermesc/%OS-BIN%/hermesc",
    ]

    // Also check pnpm store for any hermes-compiler version
    def pnpmStore = file("${rootDir}/node_modules/.pnpm")
    if (pnpmStore.exists()) {
        pnpmStore.listFiles()?.findAll { it.name.startsWith("hermes-compiler@") }?.each { hermesDir ->
            candidates.add("${hermesDir.absolutePath}/node_modules/hermes-compiler/hermesc/%OS-BIN%/hermesc")
        }
    }

    for (path in candidates) {
        def osxPath = path.replace("%OS-BIN%", "osx-bin")
        def linuxPath = path.replace("%OS-BIN%", "linux64-bin")
        if (file(osxPath).exists()) {
            return path
        }
        if (file(linuxPath).exists()) {
            return path
        }
    }

    return null
}

react {
    // Configure hermesCommand for pnpm compatibility
    def hermescPath = findHermesc()
    if (hermescPath) {
        hermesCommand = hermescPath
    }

    /* Autolinking */
    autolinkLibrariesWithApp()
}

/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = false

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = io.github.react-native-community:jsc-android-intl:2026004.+`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.algorand.perarn"

    defaultConfig {
        // Default values (will be overridden by flavors)
        applicationId "com.algorand.perarn"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode project.hasProperty('android.injected.version.code') ? project.property('android.injected.version.code').toInteger() : (System.getenv("BUILD_NUMBER") ? System.getenv("BUILD_NUMBER").toInteger() : 1)
        versionName project.hasProperty('android.injected.version.name') ? project.property('android.injected.version.name') : "7.0.0"

        // Make APP_ENV available to React Native at build time
        resValue "string", "build_config_package", "com.algorand.perarn"
    }

    // Load keystore properties for release builds
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else {
                // Fallback to debug signing if keystore.properties doesn't exist
                // This allows OSS builds to work without release keys
                storeFile file('debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }
    }

    flavorDimensions "environment"

    productFlavors {
        dev {
            dimension "environment"
            applicationId "com.algorand.perarn.staging"
            versionNameSuffix "-dev"
            resValue "string", "app_name", "Pera Dev"
            buildConfigField "String", "APP_ENV", '"development"'
        }

        staging {
            dimension "environment"
            applicationId "com.algorand.perarn.staging"
            versionNameSuffix "-staging"
            resValue "string", "app_name", "Pera Staging"
            buildConfigField "String", "APP_ENV", '"staging"'
        }

        production {
            dimension "environment"
            applicationId "com.algorand.perarn"
            resValue "string", "app_name", "Pera"
            buildConfigField "String", "APP_ENV", '"production"'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    packaging {
        jniLibs {
            pickFirst '**/libc++_shared.so'
        }
    }
}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}
